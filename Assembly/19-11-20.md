# Nov20

CF ZF SF OF AF PF: 这6个称为状态标志
DF TF IF: 这3个称为控制标志
DF:direction flag
TF:trace/trap flag
IF:interrupt flag

<img src="https://img-blog.csdn.net/20180626155457820?watermark/2/text/aHR0cHM6Ly9ibG9nLmNzZG4ubmV0L3FxXzM1ODIxOTg4/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70" alt="" /><br /><img src="https://img-blog.csdn.net/20180626155536598?watermark/2/text/aHR0cHM6Ly9ibG9nLmNzZG4ubmV0L3FxXzM1ODIxOTg4/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70" alt="" /><br /><img src="https://img-blog.csdn.net/20180626155556942?watermark/2/text/aHR0cHM6Ly9ibG9nLmNzZG4ubmV0L3FxXzM1ODIxOTg4/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70" alt="" />





**OF(overflow)**

* 标志寄存器
* 若执行完相关指令发生溢出，则of=1，反之of=0



**CF与OF**

* CF针对无符号数(将寄存器中的操作数都看作是无符号数)
* OF针对有符号数(将寄存器中的操作数都看作是有符号数)

**PF**(Parity Flag)奇偶标志

(其实没什么用)

* 运算结果第八位1的个数
  * PF=1 $\Longrightarrow$ 偶数个(无论从奇偶)
* 用作校验位

**奇偶校验**

* 奇校验
  * 最后八位里1的个数是odd (包括校验位)
* 偶校验
  * 最后八位里1的个数是even(包括校验位)



==PF仅统计低八位中的奇偶个数==

例如第一位作为校验位则有：1100 0001是对的，1100 0011是错的

```assembly
;校验，接收到的数据存到al
or al, al; 相当于把al的校验弄到PF 所以其实 `and al,al;` 也可以实现; PF = 1
jp error ; PF = 1即偶数个时跳
; jp = jpe, jnp = jpo
good:

error:
```



**AF**(Auxiliary Flag)辅助进位

用于BCD

一个16进制数表示一个十进制(勿混淆)

```assembly
11h 表示 11
59h 表示 59
BCD的优点是便于用以下方法快速分离出十位和个位:
59h >> 4 -> 5
59h & 0Fh -> 9

59h + 8 = 61h   ; AF=1, 需要调整
; 需要对上述结果作调整, 方法是对上述结果加6, 得67h
59h + 2 = 5Bh	; 虽然AF=0, 但个位超过A, 所以也需要
               	; 通过加6作调整, 得61h
59h + 0 = 59h	; AF=0, 且个位<A, 所以不需要调整
```

AF没有相关的条件跳转指令。



**daa**(Decimal Adjust for Addition)

* 后一位大于9则强行+6
* 产生进位使AF为1的情况则也强行+6

```assembly
mov ah, 11h
add ah, 9
daa ; ah = 20h
```





如图

<img src="https://img-blog.csdn.net/20170505193752326?watermark/2/text/aHR0cDovL2Jsb2cuY3Nkbi5uZXQvQXBvbGxvbl9rcmo=/font/5a6L5L2T/fontsize/400/fill/I0JBQkFCMA==/dissolve/70/gravity/SouthEast">

|      |      |      |      |      |
| ---- | ---- | ---- | ---- | ---- |
| AF   |      |      |      |      |





## 控制标志寄存器

改变控制标志位的值会改变CPU的行为

### **DF**(Direction)

* 赋值指令

  * cld (Clear Direction)
  * std (Set Direction)

* 功能说明

  * 0 $\Longrightarrow$ 正方向（由低到高）
  * 1 $\Longrightarrow$ 反方向（由高到低）

* 用途

  * 若源首地址<目标首地址，则复制按反方向。

    | **1000** | **'A'** | **1002** | **'A'** |
    | -------- | ------- | -------- | ------- |
    | **1001** | **'B'** | **1003** |         |
    | **1002** | **'C'** | **1004** |         |
    | **1003** | **'D'** | **1005** |         |
    | **1004** | **'E'** | **1006** |         |

  * 当源首地址>目标首地址时，复制时按正方向

    | **1002** | **'A'** | **1000** | **'A'** |
    | -------- | ------- | -------- | ------- |
    | **1003** | **'B'** | **1001** |         |
    | **1004** | **'C'** | **1002** |         |
    | **1005** | **'D'** | **1003** |         |
    | **1006** | **'E'** | **1004** |         |

* 复制指令

  * 指定复制类型

  * rep movsb 

  * rep movsw

  * rep movsd

  * C中

    ```c
    strcpy(target, source);      //永远按正方向复制
    strncpy(starget, source, n); //永远按正方向复制，若遇到'\0'在还没到n则指复制到'\0'
    memcpy(target, source, n);   //永远按正方向复制
    memmove(target, source, n);  //能正确处理部分重叠
    ```

    不重叠

    ```c
    int main(){
    	char a[100] = "ABC123456";
        char b[100] = "ABC123456";
        char c[100] = "ABC123456";
        char d[100] = "ABC123456";
        strcpy(a, a+3);  // a[] = {123456 \0 56 \0}
        strcpy(b+3, b);  // b[] = "ABCABCABCABC.......}
        strncpy(c+3, c, 6);  // c[] = "ABCABCABC"
        memmove(a, a+3, );
        
        
    }
    ```

    memmove()的manual

    ```c
    MEMMOVE(3)               BSD Library Functions Manual               MEMMOVE(3)
    
    NAME
         memmove -- copy byte string
    
    LIBRARY
         Standard C Library (libc, -lc)
    
    SYNOPSIS
         #include <string.h>
    
         void *
         memmove(void *dst, const void *src, size_t len);
    
    DESCRIPTION
         The memmove() function copies len bytes from string src to string dst.  The two strings may overlap; the copy is always done in a non-destructive manner.
    
    RETURN VALUES
         The memmove() function returns the original value of dst.
    
    SEE ALSO
         bcopy(3), memccpy(3), memcpy(3), strcpy(3), wmemmove(3)
    
    STANDARDS
         The memmove() function conforms to ISO/IEC 9899:1990 (``ISO C90'').
    
    BSD                              June 4, 1993                              BSD
    ```

### 

### **IF**(Interrupt)

* IF = 1允许硬件中断（cli）
* 0 禁止硬件中断（sti）

* **软件中断**
  * 形如int21h
  * 程序员写出，在内存中可以看到这个代码

* 硬件中断

  * 系统（CPU）加的，在内存里看不到

  * 由外部某个事件触发（如按下键盘）

  * Ex.

    * int 9h

      由于用户按下按钮的时间很短

    * int 8h

      clk每1/18秒使cpu暂停一下

      用于对操作系统中clk_counter++

      也可以用于实现<u>多线程(多任务)</u>

  ```assembly
  mov ax, 0
  mov bx, 1
  next:
  add ax, bx
  ;此时若用户敲键,则CPU会在此处插入一条int 9h指令并执行它
  ;int 9h的功能是读键盘编码并保存到键盘缓冲区中
  add bx, 1
  cmp bx, 100
  ;若程序已运行了1/18秒,则cpu会在此处插入一条int 8h指令
  jbe next
  ```

* 用cli和sti把一段代码包围起来可以达到该段代码在执行过程中不会被打断的效果:

  ```assembly
  cli; clear interrupt禁止硬件中断
  ...; 重要代码
  sti; set interrupt允许硬件中断
  ```

  Windows用户无权限在.exe使用，否则可能会干扰其他任务，仅Windows操作系统自己可以搞

  .sys可以使用该指令

  DOS可以



### **TF**(Trace)

* TF = 1时每运行一条就会在本条与下一条中间插入一个int1h

  ```assembly
  main:
  mov ah, 1
  int 1
  add ah, 2
  int 1
  cmp ah, 3
  int 1
  ```





## 寻址方式

### 直接寻址

### 间接寻址：\[\]中有寄存器

1. [bx]  [bp]  [si]  [di]就成了最简单的间接寻址方式
   [ax]  [cx]  [dx]  [sp]语法错误

2. [bx+si] [bx+di] [bp+si] [bp+di]
   ==注意[bx+bp]以及[si+di]是错误的。==

3. [bx+2] [bp-2] [si+1] [di-1]

4. [bx+si+2] [bx+di-2] [bp+si+1]  [bp+di-1]

   * 此种方式一般用于访问结构数组的某个元素的某个成员

   * 如：

     ```c
     struct student
     {
        char name[8];
        short int score;
     };
     struct student a[10];
     ax = a[3].score;
     /*
     设bx=&a[0], si=30[sizeof(struct student)], 则ax=a[3].score可转化成以下汇编代码:
     mov ax, [bx+si+8]; /* bx+sia[3] */
     */
     ```

     

两个寄存器相加的间接寻址方式中, bx或bp通常用来表示数组的首地址, 而si或di则用来表示下标。例如: http://10.71.45.100/bhh/arydemo.asm (将abc\[\]中每个数字+1)

这个例程其实想说的是数组可以用`abc[si]`或者`[bx+si]`来调用，其中`bx`为`offset abc`



## 端口

并非网络端口（http：80；ftp：21）

CPU $\Longleftrightarrow$ Port  $\Longleftrightarrow$ I/O

```assembly
in al, 60h; 从端口60h读取一个字节并存放到AL中
; mov ah,1; int 21h;   高层
; int 9h               中层
; in al, 60h           最底层
; 可以包括方向键，ctrl（左右编码不同），cmd等等
mov al, 2
out 70h, 
```

[例程：读取CMOS中时钟信息](http://10.71.45.100/bhh/key.asm)：http://10.71.45.100/bhh/key.asm

70h是地址，71h是数值

==这个程序值得欣赏一下==：

```assembly
;---------------------------------------
;PrtSc/SysRq: E0 2A E0 37 E0 B7 E0 AA  ;
;Pause/Break: E1 1D 45 E1 9D C5        ;
;---------------------------------------
data segment
old_9h dw 0, 0
stop   db 0
key    db 0; key=31h
phead  dw 0
key_extend  db 'KeyExtend=', 0
key_up      db 'KeyUp=', 0
key_down    db 'KeyDown=', 0
key_code    db '00h ', 0
hex_tbl     db '0123456789ABCDEF'
cr          db  0Dh, 0Ah, 0
data ends

code segment
assume cs:code, ds:data
main:
   mov ax, data
   mov ds, ax
   xor ax, ax
   mov es, ax
   mov bx, 9*4
   push es:[bx]
   pop old_9h[0]
   push es:[bx+2]
   pop old_9h[2]    ; 保存int 9h的中断向量
   cli
   mov word ptr es:[bx], offset int_9h
   mov es:[bx+2], cs; 修改int 9h的中断向量
   sti
again:
   cmp [stop], 1
   jne again        ; 主程序在此循环等待
   push old_9h[0]
   pop es:[bx]
   push old_9h[2]
   pop es:[bx+2]    ; 恢复int 9h的中断向量
   mov ah, 4Ch
   int 21h

int_9h:
   push ax
   push bx
   push cx
   push ds
   mov ax, data
   mov ds, ax       ; 这里设置DS是因为被中断的不一定是我们自己的程序
   in al, 60h       ; AL=key code
   mov [key], al
   cmp al, 0E0h
   je  extend
   cmp al, 0E1h
   jne up_or_down
extend:
   mov [phead], offset key_extend
   call output
   jmp check_esc
up_or_down:
   test al, 80h     ; 最高位==1时表示key up
   jz down
up:
   mov [phead], offset key_up
   call output
   mov bx, offset cr
   call display     ; 输出回车换行
   jmp check_esc
down:
   mov [phead], offset key_down
   call output
check_esc:   
   cmp [key], 81h   ; Esc键的key up码
   jne int_9h_iret
   mov [stop], 1
int_9h_iret:
   mov al, 20h      ; 发EOI(End Of Interrupt)信号给中断控制器，
   out 20h, al      ; 表示我们已处理当前的硬件中断(硬件中断处理最后都要这2条指令)。
                    ; 因为我们没有跳转到的old_9h，所以必须自己发EOI信号。
                    ; 如果跳到old_9h的话，则old_9h里面有这2条指令，这里就不要写。
   pop ds
   pop cx
   pop bx
   pop ax
   iret             ; 中断返回指令。从堆栈中逐个弹出IP、CS、FL。

output:
   push ax
   push bx
   push cx
   mov bx, offset hex_tbl
   mov cl, 4
   push ax   ; 设AL=31h=0011 0001
   shr al, cl; AL=03h
   xlat      ; AL = DS:[BX+AL] = '3'
   mov key_code[0], al
   pop ax
   and al, 0Fh; AL=01h
   xlat       ; AL='1'
   mov key_code[1], al
   mov bx, [phead]
   call display     ; 输出提示信息
   mov bx, offset key_code
   call display     ; 输出键码
   pop cx
   pop bx
   pop ax
   ret
   
display:
   push ax
   push bx
   push si
   mov si, bx
   mov bx, 0007h    ; BL = color
   cld
display_next:
   mov ah, 0Eh      ; AH=0Eh, BIOS int 10h的子功能，具体请查中断大全
   lodsb
   or al, al
   jz display_done
   int 10h          ; 每次输出一个字符
   jmp display_next
display_done:
   pop si
   pop bx
   pop ax
   ret
code ends
end main
```

`Test`命令将两个操作数进行逻辑与运算，并根据运算结果设置相关的标志位。



## Q

* CLI & STI会导致clk信号错误系统崩溃？
  * 重启之后系统的clk会与bios的校准