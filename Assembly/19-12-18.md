# Dec18

Q: 为什么第三次作业用600h不行：==小端规则啊==

[TOC]

## 字符串操作

### (rep) movsb(sw,sd)

在执行此指令前要做以下准备工作：

1. ds:si 指向源字符串(**si就是source index**)
2. es:di 指向目标字符串(**di就是destination index**)
3. cx=移动次数
4. DF=0即方向标志设成正方向(用指令cld)

==es:[di]与ds:[si]==

单独movsb指令所做的操作如下:

```pseudocode
byte ptr es:[di] = byte ptr ds:[si]
if(df==0)	// pos
{si++; di++;}
else		// neg
{si--; di--;}
```

**rep** movsw的操作过程:

```pseudocode
again:
if(cx == 0)
   goto done;
word ptr es:[di] = word ptr ds:[si]
if(df==0)
{si+=2; di+=2;}
else
{si-=2; di-=2;}
cx--
goto again
done:
```

例子: 要把以下左侧4个字节复制到右侧
1000:0000 'A'        2000:0000  'A'
1000:0001 'B'        2000:0001  'B'
1000:0002 'C'        2000:0002  'C'
1000:0003 00         2000:0003  00

```assembly
; 正向            反向(重叠时可能需要，防止覆盖)
mov ax, 1000h
mov ds, ax
mov si, 0      ; mov si, 3
mov ax, 2000h
mov es, ax
mov di, 0      ; mov di, 3
mov cx, 4
cld             ; std
rep movsb
; 循环结束时         循环结束时
; si=4              si=FFFF
; di=4              di=FFFF
; cx=0              cx=0

```



#### 非2、4倍数的repmovsw/d处理：And 求余数

1101b and 3 = 01

**Ex**. 在32位系统下, 假定ds:esi->源内存块, es:edi->目标块, DF=0, 则当要复制的字节数ecx不是4的倍数时，可以做如下处理:

```assembly
push ecx
shr ecx, 2
rep movsd
pop ecx
and ecx, 3; 相当于ecx = ecx % 4
rep movsb
```



### cmpsb(w,d)

比较byte ptr ds:[si]与byte ptr es:[di]

* 当DF=0时，SI++，DI++
* 当DF=1时，SI--，DI--

#### repe cmpsb;(若本次比较相等则继续比较下一个)

```assembly
again:
if(cx == 0) goto done;
比较byte ptr ds:[si]与byte ptr es:[di]
当DF=0时，SI++，DI++
当DF=1时，SI--，DI--
cx--
若本次比较相等则 goto again
done:
```

**Ex**.

设ds=1000h, si=10A0h, es=2000h, di=20F0h
cx=6, DF=0

| **1000:10A0** | 'A' |      | **2000:20F0** | **‘****A****’** |
| ------------- | --------------- | ---- | ------------- | --------------- |
| **1000:10A1** | 'B' |      | **2000:20F1** | **‘****B****’** |
| **1000:10A2** | 'C' |      | **2000:20F2** | **‘****C****’** |
| **1000:10A3** | '1' |      | **2000:20F3** | **‘****4****’** |
| **1000:10A4** | '2' |      | **2000:20F4** | **‘****5****’** |
| **1000:10A5** | '3' |      | **2000:20F5** | **‘****6****’** |

<u>全等情形单独考虑(不能只用cx来推出双方全等(最后一位有可能不相等)，用标志位(cx=0 且 zf = 1)就行了)</u>

```assembly
repe cmpsb
je equal; 全等
dec si
dec di
; ...
equal:
```

#### repne cmpsx

找出什么时候开始相等



### 字符串扫描指令 scasx

用途 寻址特殊字符，如'\0'

scasb:

```pseudocode
cmp al, es:[di]
di++; 当DF=1时，为di--
```

repne scasb:

```pseudocode
next:
  if(cx == 0) goto done;
  cmp al, es:[di]
      di++; 当DF=1时，为di--
      cx--
  je done
  goto next
done:
```

#### Not FFFFh-FFFBh



### stosx

类似于memset



### lodsx

AL=DS:[SI]
SI++;当DF=1时, 为SI--





## jmp

杀毒软件以为内存的范围0000:0000~8000:FFFF

病毒移到9000:....

A000:0000：显卡



CPU启动的第一条指令位于FFFF:0000，post(power on self test)最后会读到硬盘的第一个物理扇区即0道0头1扇区的200h个字节

word ptr 0000:01FE是55AAh



#### 近跳

* 方式
  * jmp 偏移地址或标号; 如jmp 1000h
  * jmp 16位寄存器		; 如jmp bx
  * jmp 16位变量  		; 如jmp word ptr [addr]

#### 远跳

* 方式
  * jmp 段地址:偏移地址
  * jmp dword ptr 32位变量

#### 短跳、近跳与长跳

一般情况下，编译器会自动度量跳跃的距离，因此我们在写源程序的时候不需要加上short、near ptr、far ptr等类型修饰。即上述三种写法一律可以==简化为jmp target==。

以下条件跳转指令也都属于短跳: jc jnc jo jno js jns jz jnz ja jb jae jbe jg jge jl jle jp jnp



**短跳不够时的解决方法**

```assembly
cmp ax, bx
je  equal; jump out of range
jne not_equal
jmp equal; 近跳，利用编译器自动调节jmp短近跳的功能
not_equal:
...; 假定这里省略指令的机器码总长度超过7Fh字节
equal:
...
```



   例:自我移动的代码 http://10.71.45.100/bhh/movcode.asm
   例: 修改printf让它做加法运算http://10.71.45.100/bhh/printf.c (用假的函数覆盖printf)

```c
/* 用DosBoxTc编译，菜单Options->Compiler->Model->Tiny
    Compile->Compile to OBJ
    Compile->Link EXE file
    Run->Run
    Run->User Screen
 */

extern int printf();
int f(int a, int b)
{
   return a+b;
}
void zzz(void)	// 由于函数内部设flag外面看不到，于是这里放一个没用的函数就可用(zzz-f)获得f的长度了
{
}
main()
{
   char buf[100];
   char *p = (char *)printf;
   char *q = (char *)f;
   int n = (char *)zzz - (char *)f;
   int y;
   memcpy(buf, p, n);
   memcpy(p, q, n);
   y = printf(10, 20);
   memcpy(p, buf, n);
   printf("y=%d\n", y);
}
```



综合**jump.asm**

```assembly
code segment
assume cs:code
main:
   jmp next; jmp short next
exit:
   mov ah, 4Ch
   int 21h
next:
   mov ah, 2
   mov dl, 'A'
   int 21h
   jmp abc; jmp near ptr abc
   db 200h dup(0)
abc:
   jmp far ptr away; jmp far ptr away
code ends

fff segment
assume cs:fff
away:
   mov ah, 2
   mov dl, 'F'
   int 21h
   jmp far ptr exit; jmp far ptr exit
fff ends
end main
```

